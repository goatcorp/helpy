
title: Start
position: -56,297
---

Hi! I'm Helpy, the interactive and smart XIVLauncher helper.
Please choose what you're here to get help with.

-> My game crashes or behaves strangely
    <<jump CrashesWhichType>>
-> The launcher isn't opening or behaves strangely
    <<jump Launcher>>
-> I have a question about a specific plugin
    <<jump SpecificPluginQuestion>>
//-> A plugin or Dalamud does not work as expected
//    <<jump Plugins>>
===

title: CrashesWhichType
position: 94,488
---
<<declare $UserIndicatedCrashType = 0 as number>>

What type of crash are you encountering?
-> I receive a message box stating "An error in the game has occurred"
    <<set $UserIndicatedCrashType to 1>>
-> The game closes by itself
    <<set $UserIndicatedCrashType to 2>>
-> I receive a message by the game containing Japanese text
    <<set $UserIndicatedCrashType to 3>>
-> The game closed and XIVLauncher disappeared
    <<jump Antivirus>>

<<jump CrashesUploadTsPack>>
===

title: CrashesUploadTsPack
position: 151,749
---
<<if !visited("CrashesUploadTsPack")>>
    I need some extra information about your game to help here.

    <<if $UserIndicatedCrashType == 1>>
        In the crash window, please click the "this file" link. It will prompt you to save a file containing information about the crash.
        Save this file to a place on your PC, and upload it below.

        [img src="upload_crash_tspack" alt="The Dalamud crash window, with a link used to generate a crash report circled. The link reads this file."]
    <<elseif $UserIndicatedCrashType >= 2 || $UserIndicatedCrashType == 0>>
        <<if $UserIndicatedCrashType == 2>>
            Immediately after the crash, please open the XIVLauncher main page.
        <<else>>
            Immediately after encountering the problem, please close the game, and open the XIVLauncher main page.
        <<endif>>

        If you enabled auto-login in XIVLauncher, and it automatically starts the game, you will have to hold shift while clicking the XIVLauncher icon to do this.
    <<endif>>

    <<UploadTspack>>
<<endif>>

<<if !$AlreadyRepaired && GetIndexMismatchKind() != 5>>
    Your game installation is damaged. Please try to repair your game.
    <<jump DoRepair>>
<<endif>>

<<if HasThirdPlugins() == true>>
    <<jump StartWithoutThirdPlugins>>
<<endif>>

<<jump GoToDiscord>>
===

title: StartWithoutThirdPlugins
position: 533,823
---

<<if HasSpecificPlugin("Penumbra") == true>>
    You seem to be using plugins from custom repositories, such as Penumbra, indicated by a yellow "3" icon in the plugin installer.
<<else>>
    You seem to be using plugins from custom repositories, indicated by a yellow "3" icon in the plugin installer.
<<endif>>

[img src="thirdplugins" alt="An entry of a plugin in the plugin installer, marked with a yellow 3 on its icon"/]

These plugins are not officially supported by the XIVLauncher developers and are prone to cause crashes or other strange behaviour. Please try launching without these plugins, to check if the issue still occurrs.
To do this, on the main XIVLauncher page, right click the login button and choose "Start w/o Third-Party plugins".
If you enabled auto-login in XIVLauncher, and it automatically starts the game, you will have to hold shift while clicking the XIVLauncher icon to do this.

[img src="disableunsupported" alt="The XIVLauncher window, with a mouse cursor resting on the login button. A context menu is open, with an entry stating Start without Third-party plugins underlined."/]

After starting the game in this way, please play for a while to see if you can still encounter the crash.

-> The game still crashes
    <<jump GoToDiscord>>
-> The crash is solved
    <<jump ThirdPluginSupportNeeded>>
===

title: ThirdPluginSupportNeeded
position: 858,756
---
This confirms that the crash is caused by these plugins. We sadly cannot help you figure out which specific plugin is causing this issue.
You may have to try to disable them one-by-one until you find the plugin that is causing the problem.

<<if HasSpecificPlugin("Penumbra") == true && HasSpecificPlugin("MareSynchronos") == false >>
It is also possible that the problem lies with a specific Penumbra mod you are using. You should head over to the Penumbra Discord server to ask for guidance if you can't find the mod that is causing the problem.
<<elseif HasSpecificPlugin("Penumbra") == true && HasSpecificPlugin("MareSynchronos") == true>>
It is also possible that the problem lies with a specific Penumbra mod you are using, or with a mod that is used by someone you are paired with via Mare Synchronos. You should head over to the Penumbra Discord server to ask for guidance if you can't find the mod that is causing the problem.
<<endif>>

<<set $OfferJoinDiscord to false>>
<<jump Goodbye>>
===

title: Plugins
position: 1,-41
---

go away
[img src="shibe" alt="A shibe dog"/]

===

title: Launcher
position: 257,124
---
<<declare $RepairFailedFromLauncher = false as bool>>

What's wrong with the launcher?

-> The launcher is failing to open or update
    That sucks.
-> I can't log into the game
    If you own the game through Steam, have a Security Token/One-Time Password linked, or are using the free trial, you'll need to enable the relevant checkboxes.
    For Steam users, check the "Use Steam service" checkbox on the main menu.
    For Security Token users, check the "Use One-Time-Passwords" checkbox on the main menu.
    For Free Trial users, check the "Start in free trial mode" checkbox in the "About" tab of the settings menu.
    This might also just be a general server outage. If all else fails, try checking online for the server status or waiting a while.
    <<jump Goodbye>>
-> The launcher failed to start the game
    <<jump Antivirus>>
-> The launcher says it can't start Dalamud
    This is usually caused by either your antivirus or internet connection.
    <<jump Antivirus>>
    <<jump Internet>>
-> I can't repair my game
  <<set $RepairFailedFromLauncher to true>>
  <<jump FailedRepair>>
===

title: DoRepair
position: 666,280
---
<<declare $RepairFailedFromDoRepair = false as bool>>
<<declare $AlreadyRepaired = false as bool>>

<<if !$RepairFailedFromDoRepair>>
    Your game files are corrupted and need to be repaired. This corruption may be caused by a third party program like TexTools.
    Start XIVLauncher while holding Shift, right click Log in, and select Repair game files to start a game repair.
    This process will take a few minutes - please hang tight. After it's done, all non-game files will be moved into a new folder.
<<endif>>

-> The repair fixed my issues
  <<jump Goodbye>>
-> I'm still experiencing issues
  <<set $AlreadyRepaired to true>>
  <<jump Crashes>>
-> The repair failed <<if !$RepairFailedFromDoRepair>>
  <<set $RepairFailedFromDoRepair to true>>
  <<jump FailedRepair>>
===

title: FailedRepair
position: 433,531
---
Try closing other programs related to FFXIV, like Teamcraft or TexTools.
You might need to also restart your computer.

<<if $RepairFailedFromDoRepair>>
    <<jump DoRepair>>
<<endif>>

<<if $RepairFailedFromLauncher>>
    <<jump Goodbye>>
<<endif>>
===

title: Antivirus
position: 626,-41
---
Please read [link href="faq_av"]our FAQ entry[/link] regarding permitting XIVLauncher and Dalamud in your antivirus.
Keep in mind that some antivirus software sometimes don't listen to the exclusions you make.
You might also need to allow XIVLauncher and Dalamud through your firewall if you have one.
===

title: Internet
position: 626,108
---
Please read [link href="faq_connectivity"]our FAQ entry[/link] regarding network connection issues.
===

title: SpecificPluginQuestion
position: 850,566
---
I can't help you with questions about specific plugins, but feel free to ask in the plugin-specific forum on our Discord server.
The plugin's developers will be happy to help out there.

[button href="discord"]Join our Discord server[/button]

Please keep in mind that we only support official plugins, which do not come from custom repositories, on our Discord server. If you have a question about a plugin from a custom repository, you should reach out to its developer directly.
Unsupported plugins are marked with a yellow "3" in the plugin installer.
[img src="thirdplugins" alt="An entry of a plugin in the plugin installer, marked with a yellow 3 on its icon"/]

<<stop>>
===


title: GoToDiscord
position: 850,566
---
Sorry, but it looks like I can't help you with this here.
Please join our Discord server and post your crash information, together with a log of this conversation.
You can copy the log by using the "Copy conversation to clipboard" button below.

[button href="discord"]Join our Discord server[/button]
===

title: Goodbye
position: 1202,248
---
<<declare $OfferJoinDiscord = true as bool>>

That's all the advice I can offer. Thanks for using Helpy!
If your issue isn't solved, consider trying Helpy again.

<<if $OfferJoinDiscord == true>>
    If you want more specialized help, you can also join our discord server by clicking the link below.
    It would help if you could post a log of this conversation by clicking "Copy conversation to clipboard" below.
    [button href="discord"]Join our Discord server[/button]
<<endif>>
===
